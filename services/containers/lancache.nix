{ config, pkgs, lib, cookie-pkg, ssd-mnt, nvm-mnt, nvm-name, ... }:

let
  cc-dir = "${cookie-pkg}/containers";
in
{
  imports = [
    ../../system/containers/podman.nix
    ../system/server-and-config-update.nix
    ./container-image-updater
  ];

  container-image-updater."lancache" = {
    upstream.name = "lancachenet/monolithic";
    final.name = "lancache-monolithic";
  };

  systemd = {
    services = {
      "podman-lancache" = (import ./podman-systemd-service.nix lib 17) // {
        after = [
          "mnt-${nvm-name}.mount"
        ];
        requires = [
          "mnt-${nvm-name}.mount"
        ];
      };
      "server-and-config-update@Lancache" = {
        # fixes empty unit file (extends template unit)
        overrideStrategy = "asDropin";
        # overrides.conf (generated by Nix) resets PATH variable
        path = [
          pkgs.bash
          pkgs.gawk
          pkgs.git
          pkgs.jq
          pkgs.which
        ];
        # extra changes
        description = "Updates Lancache DNS records for unbound";
        serviceConfig = {
          ReadWritePaths = [
            "${ssd-mnt}/appdata/lancache/cache-domains"
            "${ssd-mnt}/appdata/unbound/conf"
          ];
          BindReadOnlyPaths = [
            "/etc/ssl"
            "/etc/static/ssl"
          ];
        };
      };
    };

    timers."server-and-config-update@Lancache" = {
      enable = true;
      overrideStrategy = "asDropin";
      wantedBy = [ "timers.target" ];
    };
  };

  virtualisation.oci-containers.containers = {
    "lancache" = {
      autoStart = true;
      dependsOn = [
        "proxy"
      ];

      image = "localhost/lancache-monolithic:latest";
      imageFile = pkgs.dockerTools.pullImage (
        builtins.fromJSON (builtins.readFile "${cc-dir}/lancache-monolithic.json")
      );

      environment = {
        TZ = config.time.timeZone;
        LANCACHE_IP = "192.168.178.150";
        UPSTREAM_DNS = "172.23.53.2";
        CACHE_MAX_AGE = "1234d";
        CACHE_MEM_SIZE = "1000m";
        CACHE_DISK_SIZE = "4321g";
        CACHE_SLICE_SIZE = "8m";
      };
      volumes = [
        "${nvm-mnt}/appdata3/lancache/data:/data/cache:Z"
        "${nvm-mnt}/appdata3/lancache/logs:/data/logs:Z"
        "${ssd-mnt}/appdata/lancache/cache-domains:/data/cachedomains:z"
        "lancache_www:/var/www:Z"
      ];
      extraOptions = [
        "--network" "server"
        "--ip" "172.23.80.4"
        "--ip6" "fd00:172:23::443:4"
        "--dns" "172.23.53.2"
        "--dns" "fd00:172:23::aaaa:2"
      ];
    };
  };
}
